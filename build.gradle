plugins {
    id 'signing'
    id 'pl.allegro.tech.build.axion-release' version '1.13.6'
    id 'biz.aQute.bnd.builder' version "$bndVersion" apply false
}

scmVersion {
    tag {
        prefix = 'mstor-connector-'
    }
    versionCreator 'versionWithBranch'
    branchVersionCreator = [
            'main': 'simple',
    ]
    nextVersion {
        suffix = 'pre'
        separator = '-'
    }
}

configure(subprojects) {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'biz.aQute.bnd.builder'

    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
    }

    sourceCompatibility = 8
    targetCompatibility = 8

    group = 'org.mstor'
    description = '''\
A command framework for message store management'''
    version = rootProject.scmVersion.version

    ext {
        isReleaseVersion = !version.endsWith("SNAPSHOT")
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    dependencies {

        api "org.mnode.mstor:mstor:$mstorVersion"
        
        implementation ("org.eclipse.jetty:jetty-server:$jettyVersion") {
            exclude group: 'org.slf4j', module: 'slf4j-api'
        }
        implementation ("org.eclipse.jetty:jetty-servlet:$jettyVersion") {
            exclude group: 'org.slf4j', module: 'slf4j-api'
        }

        implementation "info.picocli:picocli:$picocliVersion"

        compileOnly 'org.osgi:osgi.core:8.0.0',
                'org.osgi:org.osgi.service.component.annotations:1.5.0',
                'org.osgi:org.osgi.service.metatype.annotations:1.4.1',
                'org.osgi:org.osgi.annotation:6.0.0'

        testImplementation "org.slf4j:slf4j-log4j12:$slf4jVersion",
                "org.apache.logging.log4j:log4j:$log4jVersion"
    }

    jar {
        manifest {
            attributes (
                    'Implementation-Title': 'mstor Connector',
                    'Implementation-Version': version,
                    'Implementation-Vendor': 'Ben Fortuna'
            )
        }
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    test {
        useJUnitPlatform()
    }

    javadoc {
        if (JavaVersion.current().isJava8Compatible()) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
        options {
            links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
        }
    }

    publishing {
        publications {
            "$name"(MavenPublication) {
                from components.java
                pom.withXml {
                    asNode().appendNode('name', name)
                    asNode().appendNode('description', description)
                    asNode().appendNode('url', 'http://www.mstor.org')

                    def scmNode = asNode().appendNode('scm')
                    scmNode.appendNode('url', 'https://github.com/micronode/mstor-connector')
                    scmNode.appendNode('connection', 'scm:git@github.com:micronode/mstor-connector.git')
                    scmNode.appendNode('developerConnection', 'scm:git@github.com:micronode/mstor-connector.git')

                    def licenseNode = asNode().appendNode('licenses').appendNode('license')
                    licenseNode.appendNode('name', 'mstor - License')
                    licenseNode.appendNode('url', 'https://raw.githubusercontent.com/micronode/mstor/master/LICENSE')
                    licenseNode.appendNode('distribution', 'repo')

                    def developerNode = asNode().appendNode('developers').appendNode('developer')
                    developerNode.appendNode('id', 'fortuna')
                    developerNode.appendNode('name', 'Ben Fortuna')
                }
            }
        }

        repositories {
            maven {
                name = "OSSRH"
                url = version.endsWith('SNAPSHOT') ? "https://s01.oss.sonatype.org/content/repositories/snapshots/" : "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
        }
    }

    signing {
        required { isReleaseVersion }
        sign publishing.publications[name]
    }
}
